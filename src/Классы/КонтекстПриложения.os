#Использовать semaphore

Перем ОпределенияКомпонентов;
Перем ИнициализированныеКомпоненты;
Перем ФабрикаКомпонентов;

Функция ПолучитьОпределенияКомпонентов() Экспорт
	Возврат Новый ФиксированноеСоответствие(ОпределенияКомпонентов);
КонецФункции

Функция ПолучитьОпределениеКомпонента(Имя) Экспорт
	Возврат ОпределенияКомпонентов.Получить(Имя);
КонецФункции

Функция ПолучитьКомпонент(Имя) Экспорт
	Компонент = ИнициализированныеКомпоненты.Получить(Имя);
	
	// double-lock checking
	Если Компонент = Неопределено Тогда
		Попытка
			Семафор = Семафоры.Получить(Имя);
			Семафор.Захватить();
			
			Компонент = ИнициализированныеКомпоненты.Получить(Имя);
			Если Компонент = Неопределено Тогда
				Компонент = ФабрикаКомпонентов.ПолучитьКомпонент(ЭтотОбъект, Имя);
				ИнициализированныеКомпоненты.Вставить(Имя, Компонент);
			КонецЕсли;
			
			Семафор.Освободить();
		Исключение
			Семафор.Освободить();
		КонецПопытки;
	КонецЕсли;

	Возврат Компонент;
КонецФункции

Процедура ЗарегистрироватьКомпонент(Тип, Знач Имя = "") Экспорт

	Если Не ЗначениеЗаполнено(Имя) Тогда
		Имя = Строка(Тип);
	КонецЕсли;

	ОпределениеКомпонента = Новый ОпределениеКомпонента(Тип, Имя);

	ОпределенияКомпонентов.Вставить(ОпределениеКомпонента.Имя(), ОпределениеКомпонента);

КонецПроцедуры

Процедура ПриСозданииОбъекта()
	ОпределенияКомпонентов = Новый Соответствие();
	ИнициализированныеКомпоненты = Новый Соответствие();
	ФабрикаКомпонентов = Новый ФабрикаКомпонентов();
КонецПроцедуры
