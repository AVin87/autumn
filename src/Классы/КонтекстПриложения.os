#Использовать logos
#Использовать semaphore

Перем ИнициализированныеКомпоненты;
Перем ФабрикаКомпонентов;

Перем Лог;

Функция ПолучитьОпределенияКомпонентов() Экспорт
	Возврат ФабрикаКомпонентов.ПолучитьОпределенияКомпонентов();
КонецФункции

Функция ПолучитьОпределениеКомпонента(Имя) Экспорт
	Возврат ФабрикаКомпонентов.ПолучитьОпределениеКомпонента(Имя);
КонецФункции

Функция ПолучитьКомпонент(Имя) Экспорт
	Компонент = ИнициализированныеКомпоненты.Получить(Имя);
	
	// double-lock checking
	Если Компонент = Неопределено Тогда
		Попытка
			Семафор = Семафоры.Получить(Имя);
			Семафор.Захватить();
			
			Компонент = ИнициализированныеКомпоненты.Получить(Имя);
			Если Компонент = Неопределено Тогда
				Компонент = ФабрикаКомпонентов.ПолучитьКомпонент(ЭтотОбъект, Имя);
				ИнициализированныеКомпоненты.Вставить(Имя, Компонент);
			КонецЕсли;
			
			Семафор.Освободить();
		Исключение
			Лог.Ошибка("Не удалось инициализировать желудь %1", Имя);
			Лог.Ошибка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Семафор.Освободить();
		КонецПопытки;
	КонецЕсли;

	Возврат Компонент;
КонецФункции

Процедура ЗарегистрироватьКомпонент(Тип, Имя = "") Экспорт

	ФабрикаКомпонентов.ЗарегистрироватьКомпонент(Тип, Имя);

КонецПроцедуры

Процедура ПриСозданииОбъекта()
	ИнициализированныеКомпоненты = Новый Соответствие();
	ФабрикаКомпонентов = Новый ФабрикаКомпонентов();

	Лог = Логирование.ПолучитьЛог("oscript.lib.autumn.application.context");
КонецПроцедуры
