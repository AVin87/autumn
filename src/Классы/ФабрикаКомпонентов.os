#Использовать asserts
#Использовать reflector
#Использовать "../internal"

Перем ОпределенияКомпонентов;

Функция ПолучитьОпределенияКомпонентов() Экспорт
	Возврат Новый ФиксированноеСоответствие(ОпределенияКомпонентов);
КонецФункции

Функция ПолучитьОпределениеКомпонента(Имя) Экспорт
	Возврат ОпределенияКомпонентов.Получить(Имя);
КонецФункции

Процедура ЗарегистрироватьКомпонент(ТипКомпонента, Знач Имя) Экспорт

	Если Не ЗначениеЗаполнено(Имя) Тогда
		Имя = Строка(ТипКомпонента);
	КонецЕсли;

	РефлекторОбъекта = Новый РефлекторОбъекта(ТипКомпонента);

	Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов("Желудь");
	Ожидаем.Что(Методы.Количество(), "Компонент должен иметь ровно один метод с аннотацией &Желудь").Равно(1);

	Конструктор = Методы[0];
	МаркиПластилина = Новый Массив;
	Для Каждого ПараметрКонструктора Из Конструктор.Параметры Цикл

		АннотацииПараметра = ПараметрКонструктора.Аннотации;
		АннотацииЗависимость = АннотацииПараметра.НайтиСтроки(Новый Структура("Имя", "пластилин"));
		
		Ожидаем.Что(АннотацииЗависимость.Количество()).Равно(1);

		АннотацияЗависимость = АннотацииЗависимость[0];

		ПараметрыАннотации = АннотацияЗависимость.Параметры;
		Ожидаем.Что(ПараметрыАннотации.Количество()).Равно(1);

		ИмяПараметра = ПараметрыАннотации[0].Значение;
		
		МаркиПластилина.Добавить(ИмяПараметра);

	КонецЦикла;

	Действие = Новый Действие(ФабричныеМетоды, "КонструкторОбъекта");
	Завязь = Новый СвойстваЗавязи(Действие, Истина);

	ОпределениеКомпонента = Новый ОпределениеКомпонента(ТипКомпонента, Имя, МаркиПластилина, Завязь);

	ОпределенияКомпонентов.Вставить(Имя, ОпределениеКомпонента);

КонецПроцедуры

Функция ПолучитьКомпонент(КонтекстПриложения, ИмяКомпонента) Экспорт

	ОпределениеКомпонента = КонтекстПриложения.ПолучитьОпределениеКомпонента(ИмяКомпонента);

	Ожидаем.Что(
		ОпределениеКомпонента, 
		СтрШаблон("Не удалось получить определение компонента по имени компонента %1", ИмяКомпонента)
	).Не_().Равно(Неопределено);

	КускиПластилина = Новый Массив;
	Для Каждого МаркиПластилина Из ОпределениеКомпонента.МаркиПластилина() Цикл
		КусокПластилина = КонтекстПриложения.ПолучитьКомпонент(МаркиПластилина);
		КускиПластилина.Добавить(КусокПластилина);
	КонецЦикла;

	Завязь = ОпределениеКомпонента.Завязь();
	Действие = Завязь.Действие();
	Если Завязь.ЭтоКонструктор() Тогда
		Желудь = Действие.Выполнить(ОпределениеКомпонента.ТипКомпонента(), КускиПластилина);
	Иначе
		Желудь = Новый Рефлектор().ВызватьМетод(Действие, "Выполнить", КускиПластилина);
	КонецЕсли;

	Возврат Желудь;
	
КонецФункции

Процедура ПриСозданииОбъекта()
	ОпределенияКомпонентов = Новый Соответствие();
	
КонецПроцедуры