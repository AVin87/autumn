Перем Имя Экспорт;

Перем ТипЖителяЛеса Экспорт;

Перем Параметры Экспорт;

Перем Аннотации Экспорт;

Функция Параметры() Экспорт
	Возврат Параметры.Скопировать();
КонецФункции

Процедура Проверить(Аннотация) Экспорт
	Для Каждого Параметр Из Параметры Цикл
		ПараметрАннотации = РаботаСАннотациями.ПолучитьПараметрАннотации(Аннотация, Параметр.Имя);
		Если ПараметрАннотации = Неопределено И НЕ Параметр.ЕстьЗначениеПоУмолчанию Тогда
			ВызватьИсключение СтрШаблон(
				"Аннотация ""%1"" должна содержать параметр ""%2""",
				Аннотация.Имя,
				Параметр.Имя
			);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПриСозданииОбъекта(Знач _Имя, Знач _ТипЖителяЛеса, Знач _Параметры, Знач _Аннотации)

	Имя = _Имя;
	ТипЖителяЛеса = _ТипЖителяЛеса;
	Аннотации = _Аннотации;
	Параметры = Новый ТаблицаЗначений();
	Параметры.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Параметры.Колонки.Добавить("ЕстьЗначениеПоУмолчанию", Новый ОписаниеТипов("Булево"));
	Параметры.Колонки.Добавить("ЗначениеПоУмолчанию");

	Для Каждого Параметр Из _Параметры Цикл
		АннотацияПараметр = РаботаСАннотациями.НайтиАннотацию(Параметр.Аннотации, "Параметр");
		Если АннотацияПараметр = Неопределено Тогда
			ВызватьИсключение СтрШаблон(
				"Параметр ""%1"" в конструкторе классе ""%2"" не содержит аннотацию ""Параметр""",
				Параметр.Имя,
				Имя	
			);
		КонецЕсли;

		СтрокаПараметр = Параметры.Добавить();
		СтрокаПараметр.Имя = Параметр.Имя;
		ЗначениеПоУмолчанию = АннотацияПараметр.Параметры.Найти("ЗначениеПоУмолчанию", "Имя");
		СтрокаПараметр.ЕстьЗначениеПоУмолчанию = ЗначениеПоУмолчанию <> Неопределено;
		Если СтрокаПараметр.ЕстьЗначениеПоУмолчанию Тогда
			СтрокаПараметр.ЗначениеПоУмолчанию = ЗначениеПоУмолчанию.Значение;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры