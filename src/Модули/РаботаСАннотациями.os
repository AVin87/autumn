#Использовать fluent

Функция ПолучитьАннотацию(Свойство, ИмяАннотации) Экспорт
	Аннотация = ПроцессорыКоллекций.ИзКоллекции(Свойство.Аннотации)
		.Фильтровать(
			"Результат = ВРег(Элемент.Имя) = ВРег(ДополнительныеПараметры.ИмяАннотации)", 
			Новый Структура("ИмяАннотации", ИмяАннотации)
		).ПолучитьПервый();

	Возврат Аннотация;
КонецФункции

Функция ПолучитьЗначениеПараметраАннотации(
	Аннотация, 
	ИмяПараметра = "Значение", 
	ЗначениеПоУмолчанию = Неопределено
) Экспорт

	ИмяПараметраПоУмолчанию = "Значение";

	Если Аннотация.Параметры = Неопределено ИЛИ Аннотация.Параметры.Количество() = 0 Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;

	ПараметрАннотации = ПроцессорыКоллекций.ИзКоллекции(Аннотация.Параметры)
		.Фильтровать(
			"Результат = ВРег(Элемент.Имя) = ВРег(ДополнительныеПараметры.ИмяПараметра)", 
			Новый Структура("ИмяПараметра", ИмяПараметра)
		).ПолучитьПервый();

	Если ПараметрАннотации <> Неопределено Тогда
		Возврат ПараметрАннотации.Значение;
	КонецЕсли;

	Если ИмяПараметра = ИмяПараметраПоУмолчанию И Аннотация.Параметры.Количество() > 1 Тогда
		Сообщение = СтрШаблон(
			"Аннотация %1 содержит несколько параметров, среди которых нет явного указания имени параметра ""%2""",
			Аннотация.Имя,
			ИмяПараметраПоУмолчанию
		);
		ВызватьИсключение Сообщение;
	ИначеЕсли ИмяПараметра = ИмяПараметраПоУмолчанию И Аннотация.Параметры[0].Имя = Неопределено Тогда
		Возврат Аннотация.Параметры[0].Значение;
	Иначе
		Сообщение = СтрШаблон("Не найден параметр аннотации %1", ИмяПараметра);
		ВызватьИсключение Сообщение; 
	КонецЕсли;

КонецФункции