#Использовать asserts
#Использовать fluent
#Использовать reflector

Перем ОпределенияАннотаций;
Перем ОпределенияЖелудейПоИмени;
Перем ОпределенияЖелудейПоПрозвищу;
Перем ОпределенияНапильников;
Перем ИнициализируемыеНапильники;

Функция ПолучитьОпределенияЖелудей() Экспорт
	Возврат Новый ФиксированноеСоответствие(ОпределенияЖелудейПоИмени);
КонецФункции

Функция ПолучитьОпределениеЖелудя(Имя) Экспорт
	НайденноеОпределение = ОпределенияЖелудейПоИмени.Получить(Имя);
	Если НайденноеОпределение <> Неопределено Тогда
		Возврат НайденноеОпределение;
	КонецЕсли;

	НайденныеОпределения = ОпределенияЖелудейПоПрозвищу.Получить(Имя);
	Если НайденныеОпределения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если НайденныеОпределения.Количество() = 1 Тогда
		Возврат НайденныеОпределения[0];
	КонецЕсли;

	НайденноеОпределение = НайтиОпределениеВерховного(НайденныеОпределения);
	
	Если НайденноеОпределение = Неопределено Тогда
		ВызватьИсключение "Найдено несколько желудей с именем/прозвищем """ + Имя + """";
	КонецЕсли;

	Возврат НайденноеОпределение;
КонецФункции

Функция НайтиОпределениеВерховного(Коллекция)
	Для Каждого Элемент из Коллекция Цикл
		Если Элемент.Верховный() = Истина Тогда
			Возврат Элемент; 
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

Функция ПолучитьСписокОпределенийЖелудей(Имя) Экспорт

	Результат = Новый Массив;

	НайденноеОпределение = ОпределенияЖелудейПоИмени.Получить(Имя);
	Если НайденноеОпределение <> Неопределено Тогда
		Результат.Добавить(НайденноеОпределение);
		Возврат Новый ФиксированныйМассив(Результат);
	КонецЕсли;

	НайденныеОпределения = ОпределенияЖелудейПоПрозвищу.Получить(Имя);
	
	Если НайденныеОпределения <> Неопределено Тогда
		Для Каждого Определение Из НайденныеОпределения Цикл
			Результат.Добавить(Определение);
		КонецЦикла;
	КонецЕсли;

	Возврат Новый ФиксированныйМассив(Результат);

КонецФункции

Процедура ПроинициализироватьНапильники(Поделка) Экспорт
	Для Каждого ОпределениеНапильника Из ОпределенияНапильников Цикл
		Поделка.НайтиЖелудь(ОпределениеНапильника.Имя);
	КонецЦикла;
КонецПроцедуры

Функция ДобавитьЖелудь(ТипЖелудя, Имя) Экспорт

	ОпределениеЖелудя = ДобавитьЖителяЛеса(ТипЖелудя, Имя, "Желудь");

	Возврат ОпределениеЖелудя;

КонецФункции

Функция ДобавитьДуб(ТипДуба) Экспорт

	ОпределениеЖелудя = ДобавитьЖителяЛеса(ТипДуба, "", "Дуб");

	РефлекторОбъекта = Новый РефлекторОбъекта(ТипДуба);

	МетодыЗавязи = РефлекторОбъекта.ПолучитьТаблицуМетодов("Завязь");

	Для Каждого МетодЗавязи Из МетодыЗавязи Цикл
		
		Аннотации = МетодЗавязи.Аннотации.СкопироватьКолонки();
		РазвернутьАннотации(МетодЗавязи.Аннотации, Аннотации);
		
		ТипЖелудяЗавязи = ПрочитатьТипЖелудя(МетодЗавязи, Аннотации);
		ИмяЖелудяЗавязи = ПрочитатьИмяЖелудя(Аннотации, "Завязь", МетодЗавязи.Имя);
		ХарактерЖелудя = ПрочитатьХарактерЖелудя(Аннотации);
		ПрилепляемыеЧастицы = ПрочитатьПрилепляемыеЧастицыВМетоде(МетодЗавязи);
		Завязь = СоздатьЗавязьЧерезМетодЗавязи(ТипДуба, МетодЗавязи);
		Прозвища = ПрочитатьПрозвища(Аннотации, ИмяЖелудяЗавязи);
		Верховный = ПрочитатьПризнакВерховногоЖелудя(Аннотации);
		ВремениИнициализации = Ложь;

		ОпределениеЗавязи = Новый ОпределениеЖелудя(
			ТипЖелудяЗавязи,
			ИмяЖелудяЗавязи,
			ХарактерЖелудя,
			ПрилепляемыеЧастицы,
			Завязь,
			Прозвища,
			Верховный,
			ВремениИнициализации
		);
		СохранитьОпределениеЖелудя(ОпределениеЗавязи);

	КонецЦикла;

	Возврат ОпределениеЖелудя;

КонецФункции

Функция ДобавитьРогатку(ТипРогатки) Экспорт
	ОпределениеРогатки = ДобавитьЖителяЛеса(ТипРогатки, "", "Рогатка");

	РефлекторОбъекта = Новый РефлекторОбъекта(ТипРогатки);
	
	Ожидаем
		.Что(
			РефлекторОбъекта.ЕстьПроцедура("ПриЗапускеПриложения", 0),
			"Рогатка должна иметь процедуру ПриЗапускеПриложения()"
		)
		.ЭтоИстина();

	Возврат ОпределениеРогатки;
КонецФункции

Функция ДобавитьЗаготовку(ТипЗаготовки) Экспорт
	ОпределениеЗаготовки = ДобавитьЖителяЛеса(ТипЗаготовки, "", "Заготовка");

	Для Каждого ПриклепляемаяЧастица Из ОпределениеЗаготовки.ПрилепляемыеЧастицы() Цикл
		Ожидаем
			.Что(ПриклепляемаяЧастица.ТипЧастицы(), "К заготовке могут прилепляться только детальки.")
			.Равно(ТипыПрилепляемыхЧастиц.Деталька());
	КонецЦикла;

	РефлекторОбъекта = Новый РефлекторОбъекта(ТипЗаготовки);
	
	Ожидаем
		.Что(
			РефлекторОбъекта.ЕстьПроцедура("ПриИнициализацииПоделки", 1),
			"Заготовка должна иметь процедуру ПриИнициализацииПоделки(Поделка)"
		)
		.ЭтоИстина();

	Возврат ОпределениеЗаготовки;
КонецФункции

Функция ДобавитьНапильник(ТипНапильника) Экспорт
	ОпределениеНапильника = ДобавитьЖителяЛеса(ТипНапильника, "", "Напильник");

	ДобавитьОпределениеНапильника(ОпределениеНапильника);

	Возврат ОпределениеНапильника;
КонецФункции

Функция ДобавитьСистемныйНапильник(ТипНапильника) Экспорт
	ОпределениеНапильника = ДобавитьЖителяЛеса(ТипНапильника, "", "Напильник");

	ДобавитьОпределениеНапильника(ОпределениеНапильника, Истина);

	Возврат ОпределениеНапильника;
КонецФункции

Функция ДобавитьАннотацию(ТипАннотации) Экспорт
	
	ИмяТипаАннотации = Строка(ТипАннотации);

	РефлекторОбъекта = Новый РефлекторОбъекта(ТипАннотации);
	Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов("Аннотация", Ложь);
	Ожидаем.Что(
		Методы.Количество(),
		"Класс должен иметь ровно один метод с аннотацией &Аннотация"
	).Равно(1);

	Конструктор = Методы[0];
	ОсновнаяАннотация = РаботаСАннотациями.ПолучитьАннотацию(Конструктор, "Аннотация");
	
	ИмяАннотации = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(ОсновнаяАннотация, , ИмяТипаАннотации);

	ТипЖителяЛеса = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
		ОсновнаяАннотация,
		"Тип",
		Неопределено,
		Истина
	);

	Параметры = Конструктор.Параметры;
	КоличествоПараметровСИменемПоУмолчанию = 0;
	Для Каждого Параметр Из Параметры Цикл
		Если ВРег(Параметр.Имя) = ВРег("Значение") Тогда
			КоличествоПараметровСИменемПоУмолчанию = КоличествоПараметровСИменемПоУмолчанию + 1;

			Если КоличествоПараметровСИменемПоУмолчанию > 1 Тогда
				ВызватьИсключение СтрШаблон(
					"Аннотация %1 имеет более одного параметра с именем ""Значение""",
					ИмяАннотации
				);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Аннотации = Конструктор.Аннотации;

	Для Каждого ВложеннаяАннотация Из Конструктор.Аннотации Цикл
		ОпределениеВложеннойАннотации = ОпределенияАннотаций.Получить(ВложеннаяАннотация.Имя);
		Если ОпределениеВложеннойАннотации = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		// Приведение имен параметров по умолчанию
		Для Каждого ПараметрВложеннойАннотации Из ВложеннаяАннотация.Параметры Цикл
			Если ПараметрВложеннойАннотации.Имя = Неопределено Тогда
				ПараметрВложеннойАннотации.Имя = "Значение";
			КонецЕсли;
		КонецЦикла;

		// Проверка параметров аннотации на корректность
		КоличествоПараметровСИменемПоУмолчанию = 0;
		Для Каждого ПараметрВложеннойАннотации Из ВложеннаяАннотация.Параметры Цикл
	
			ИмяПараметраВложеннойАннотации = ПараметрВложеннойАннотации.Имя;
			Если ВРег(ИмяПараметраВложеннойАннотации) = ВРег("Значение") Тогда
				КоличествоПараметровСИменемПоУмолчанию = КоличествоПараметровСИменемПоУмолчанию + 1;
			КонецЕсли;

			// Аннотация содержит только один параметр с именем по умолчанию
			Если КоличествоПараметровСИменемПоУмолчанию > 1 Тогда
				ВызватьИсключение СтрШаблон(
					"Аннотация %1 в классе %2 имеет более одного параметра без имени или с именем ""Значение""",
					ИмяАннотации,
					ИмяТипаАннотации
				);
			КонецЕсли;
			
			ОпределениеПараметраАннотации = ОпределениеВложеннойАннотации.Параметры().Найти(ИмяПараметраВложеннойАннотации, "Имя");

			// Аннотация не содержит неизвестных параметров
			Если ОпределениеПараметраАннотации = Неопределено Тогда
				ВызватьИсключение СтрШаблон(
					"Аннотация %1 в классе %2 содержит неизвестный параметр %3", 
					ВложеннаяАннотация.Имя,
					ИмяТипаАннотации,
					ИмяПараметраВложеннойАннотации
				);
			КонецЕсли;

		КонецЦикла;

		// Аннотация содержит только известные параметры
		Для Каждого ОпределениеПараметраАннотации Из ОпределениеВложеннойАннотации.Параметры() Цикл
			
			ПараметрВложеннойАннотации = ВложеннаяАннотация.Параметры.Найти(ОпределениеПараметраАннотации.Имя, "Имя");
			Если НЕ ОпределениеПараметраАннотации.ЕстьЗначениеПоУмолчанию
				И ПараметрВложеннойАннотации = Неопределено Тогда
				
				ВызватьИсключение СтрШаблон(
					"Аннотация %1 в классе %2 не содержит параметр %3",
					ИмяАннотации,
					ИмяТипаАннотации,
					ОпределениеПараметраАннотации.Имя
				);
			КонецЕсли;

			// Параметры заполнены правильными типами
			// todo: проверка типов

		КонецЦикла;
	КонецЦикла;

	ОпределениеАннотации = Новый ОпределениеАннотации(ИмяАннотации, ТипЖителяЛеса, Параметры, Аннотации);

	ОпределенияАннотаций.Вставить(НРег(ИмяАннотации), ОпределениеАннотации);

	Возврат ОпределениеАннотации;

КонецФункции

Функция НайтиЖелудь(Поделка, ИмяЖелудя, ПрилепляемыеЧастицы) Экспорт

	ОпределениеЖелудя = Поделка.ПолучитьОпределениеЖелудя(ИмяЖелудя);

	Если ОпределениеЖелудя = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Не удалось получить определение Желудя по имени Желудя %1", ИмяЖелудя);
	КонецЕсли;

	СписокНапильников = ИнициализируемыеНапильники.Получить(Поделка);
	Если СписокНапильников = Неопределено Тогда
		СписокНапильников = Новый Массив;
		ИнициализируемыеНапильники.Вставить(Поделка, СписокНапильников);
	КонецЕсли;
	ЭтоНапильник = ОпределенияНапильников.Найти(ОпределениеЖелудя.Имя(), "Имя") <> Неопределено;

	Если ЭтоНапильник Тогда
		Если СписокНапильников.Найти(ОпределениеЖелудя.Имя()) = Неопределено Тогда
			СписокНапильников.Добавить(ОпределениеЖелудя.Имя());
		КонецЕсли;
	КонецЕсли;

	ПереданныеПрилепляемыеЧастицы = ПрилепляемыеЧастицы;
	Если ПереданныеПрилепляемыеЧастицы = Неопределено Тогда
		ПереданныеПрилепляемыеЧастицы = Новый Массив;
	КонецЕсли;

	Если ПереданныеПрилепляемыеЧастицы.Количество() = ОпределениеЖелудя.ПрилепляемыеЧастицы().Количество() Тогда
		ПередаваемыеПрилепляемыеЧастицы = ПереданныеПрилепляемыеЧастицы;
	Иначе
		
		КоличествоБлестяшек = ПосчитатьКоличествоБлестяшек(ОпределениеЖелудя.ПрилепляемыеЧастицы());

		Если КоличествоБлестяшек <> ПереданныеПрилепляемыеЧастицы.Количество() Тогда
			ВызватьИсключение "При поиске желудя " + ИмяЖелудя + " количество переданных произвольных параметров отличается от количества параметров не-желудей/не-деталек.";
		КонецЕсли;

		СчетчикИспользованияБлестяшек = 0;
		ПередаваемыеПрилепляемыеЧастицы = Новый Массив;
		Для Каждого ДанныеОПрилепляемойЧастице Из ОпределениеЖелудя.ПрилепляемыеЧастицы() Цикл
			
			Если ДанныеОПрилепляемойЧастице.ТипЧастицы() = ТипыПрилепляемыхЧастиц.Блестяшка() Тогда
				ПрилепляемаяЧастица = ПереданныеПрилепляемыеЧастицы[СчетчикИспользованияБлестяшек];
				СчетчикИспользованияБлестяшек = СчетчикИспользованияБлестяшек + 1;
			Иначе
				ПрилепляемаяЧастица = ПрилеплениеЧастиц.НайтиПрилепляемуюЧастицу(Поделка, ДанныеОПрилепляемойЧастице);
			КонецЕсли;

			ПередаваемыеПрилепляемыеЧастицы.Добавить(ПрилепляемаяЧастица);
		КонецЦикла;
	КонецЕсли;

	Завязь = ОпределениеЖелудя.Завязь();
	
	Действие = Завязь.Действие();
	Если Завязь.ЭтоКонструктор() Тогда
		Желудь = Действие.Выполнить(ОпределениеЖелудя.ТипЖелудя(), ПередаваемыеПрилепляемыеЧастицы);
	Иначе
		Желудь = Действие.Выполнить(Поделка, Завязь.Родитель(), Завязь.ИмяМетода(), ПередаваемыеПрилепляемыеЧастицы);
	КонецЕсли;

	Если ЭтоНапильник Тогда
		ИндексНапильника = СписокНапильников.Найти(ОпределениеЖелудя.Имя());
		СписокНапильников.Удалить(ИндексНапильника);
	Иначе
		Если НЕ ОпределениеЖелудя.ВремениИнициализации() Тогда
			Для Каждого ОпределениеНапильника Из ОпределенияНапильников Цикл
				
				Если ОпределениеНапильника.Имя = ОпределениеЖелудя.Имя() Тогда
					Продолжить;
				КонецЕсли;
				Если СписокНапильников.Найти(ОпределениеНапильника.Имя) <> Неопределено Тогда
					// TODO: Сообщение о пропуске запуска напильника на желуде из-за циклической зависимости
					Продолжить;
				КонецЕсли;
				
				Напильник = Поделка.НайтиЖелудь(ОпределениеНапильника.Имя);
				Желудь = Напильник.ОбработатьЖелудь(Желудь, ОпределениеЖелудя);
			
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

	Возврат Желудь;
	
КонецФункции

Функция ПосчитатьКоличествоБлестяшек(ПрилепляемыеЧастицы)
	Количество = 0;
	Для Каждого Элемент из ПрилепляемыеЧастицы Цикл
		Если Элемент.ТипЧастицы() = ТипыПрилепляемыхЧастиц.Блестяшка() Тогда
			Количество = Количество + 1;	
		КонецЕсли;
	КонецЦикла;
	Возврат Количество;
КонецФункции

Функция ДобавитьЖителяЛеса(ТипЖителяЛеса, ИмяЖителяЛеса, АннотацияНадКонструктором)

	РефлекторОбъекта = Новый РефлекторОбъекта(ТипЖителяЛеса);
	АннотацияНадКонструкторомКаноническая = НРег(АннотацияНадКонструктором);
	УсловияПоиска = Новый Структура("Имя", АннотацияНадКонструкторомКаноническая);

	Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов(Неопределено, Ложь);
	Конструктор = Неопределено;
	Аннотации = Неопределено;
	
	Для Каждого Метод Из Методы Цикл
		Аннотации = Метод.Аннотации.СкопироватьКолонки();
		РазвернутьАннотации(Метод.Аннотации, Аннотации);

		НайденныеСтроки = Аннотации.НайтиСтроки(УсловияПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;

		Если НайденныеСтроки.Количество() > 1 Тогда
			ВызватьИсключение СтрШаблон(
				"Над методом ""%1"" жителя леса с типом ""%2"" найдено более одной аннотации ""%3"".",
				Метод.Имя,
				ТипЖителяЛеса,
				АннотацияНадКонструктором
			);
		КонецЕсли;
		
		Конструктор = Метод;
		Прервать;
	КонецЦикла;

	Если Конструктор = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
			"Не найден метод жителя леса типа ""%1"" с аннотацией ""%2"".",
			ТипЖителяЛеса,
			АннотацияНадКонструктором
		);
	КонецЕсли;

	// TODO: ? Надо ли оставлять эту проверку?

	// Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов(АннотацияНадКонструктором, Ложь);
	// Ожидаем.Что(
	// 	Методы.Количество(), 
	// 	"Класс должен иметь ровно один метод с аннотацией &" + АннотацияНадКонструктором
	// ).Равно(1);

	ОпределениеЖелудя = СоздатьОпределениеЖелудя(ТипЖителяЛеса, Конструктор, Аннотации, АннотацияНадКонструктором, ИмяЖителяЛеса);
	СохранитьОпределениеЖелудя(ОпределениеЖелудя);

	Возврат ОпределениеЖелудя;

КонецФункции

Процедура РазвернутьАннотации(Аннотации, НакопленныеАннотации)
	
	Для Каждого Аннотация Из Аннотации Цикл

		ОпределениеАннотации = ОпределенияАннотаций.Получить(НРег(Аннотация.Имя));

		Если ОпределениеАннотации = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		НоваяАннотация = НакопленныеАннотации.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяАннотация, Аннотация);
		
		Если НРег(Аннотация.Имя) <> НРег("Аннотация") Тогда
			РазвернутьАннотации(ОпределениеАннотации.Аннотации, НакопленныеАннотации);
			Продолжить;
		КонецЕсли;

		ТипЖелудя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Тип", Неопределено, Истина);
		Если ТипЖелудя <> Неопределено Тогда
			// Проверим, что корневая аннотация была добавлена на уровне выше
			Если РаботаСАннотациями.НайтиАннотацию(НакопленныеАннотации, ТипЖелудя) = Неопределено Тогда

				ОпределениеАннотацииТипаЖелудя = ОпределенияАннотаций.Получить(НРег(ТипЖелудя));
				
				НоваяАннотация = НакопленныеАннотации.Добавить();
				НоваяАннотация.Имя = НРег(ТипЖелудя);
				НоваяАннотация.Параметры = Аннотация.Параметры.СкопироватьКолонки();
				
				// todo: параметры корневой аннотации ("значение", "порядок"...)
				// todo: рефлектор оскрипта не отдает значения по умолчанию для параметров метода
				// ПараметрЗначение = НоваяАннотация.Параметры.Добавить();
				Для Каждого ПараметрМетодаАннотации Из ОпределениеАннотацииТипаЖелудя.Параметры() Цикл
					Если НРег(ПараметрМетодаАннотации.Имя) = НРег("Значение") Тогда
						// Корневое "значение" не добавляем, т.к. оно может быть переопределено в фабрике желудей
						Продолжить;
					КонецЕсли;
					ПараметрЗначение = НоваяАннотация.Параметры.Добавить();
					ПараметрЗначение.Имя = ПараметрМетодаАннотации.Имя;
					// todo: Значение параметра?
				КонецЦикла;

			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция СоздатьОпределениеЖелудя(ТипЖелудя, Конструктор, Аннотации, АннотацияНадКонструктором, Знач ИмяЖелудя = "")
	
	Если Не ЗначениеЗаполнено(ИмяЖелудя) Тогда
		ИмяЖелудя = ПрочитатьИмяЖелудя(Аннотации, АннотацияНадКонструктором, Строка(ТипЖелудя));
	КонецЕсли;

	ПрилепляемыеЧастицы = ПрочитатьПрилепляемыеЧастицыВМетоде(Конструктор);
	Характер = ПрочитатьХарактерЖелудя(Аннотации);
	Завязь = СоздатьЗавязьЧерезКонструкторОбъекта(ТипЖелудя, Конструктор);
	Прозвища = ПрочитатьПрозвища(Аннотации, ИмяЖелудя);
	Верховный = ПрочитатьПризнакВерховногоЖелудя(Аннотации);
	ВремениИнициализации = АннотацияНадКонструктором = "Заготовка";

	ОпределениеЖелудя = Новый ОпределениеЖелудя(
		ТипЖелудя,
		ИмяЖелудя,
		Характер,
		ПрилепляемыеЧастицы,
		Завязь,
		Прозвища,
		Верховный,
		ВремениИнициализации
	);

	Возврат ОпределениеЖелудя;

КонецФункции

Функция СоздатьЗавязьЧерезКонструкторОбъекта(ТипЖелудя, Конструктор)
	
	Действие = Новый Действие(ФабричныеМетоды, "КонструкторОбъекта");
	Завязь = Новый Завязь(Строка(ТипЖелудя), Конструктор.Имя, Конструктор, Действие, Истина);

	Возврат Завязь;

КонецФункции

Функция СоздатьЗавязьЧерезМетодЗавязи(ТипДуба, МетодЗавязи)

	Действие = Новый Действие(ФабричныеМетоды, "МетодЗавязи");
	Завязь = Новый Завязь(Строка(ТипДуба), МетодЗавязи.Имя, МетодЗавязи, Действие, Ложь);

	Возврат Завязь;

КонецФункции

Функция ПрочитатьИмяЖелудя(Аннотации, АннотацияНадМетодом, ЗначениеПоУмолчанию)

	Аннотация = РаботаСАннотациями.НайтиАннотацию(Аннотации, АннотацияНадМетодом);
	ИмяЖелудя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, , ЗначениеПоУмолчанию);

	Возврат ИмяЖелудя;

КонецФункции

Функция ПрочитатьТипЖелудя(Метод, Аннотации)

	Аннотация = РаботаСАннотациями.НайтиАннотацию(Аннотации, "Завязь");
	ТипЖелудя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
		Аннотация,
		"Тип"
	);

	Если ТипЖелудя = Неопределено Тогда
		ТипЖелудя = Метод.Имя;
	КонецЕсли;

	Попытка
		РеальныйТип = Тип(ТипЖелудя);
	Исключение
		ВызватьИсключение "Тип желудя в Завязи " + Метод.Имя + " не известен. Укажите тип желудя в аннотации или переименуйте метод завязи.";
	КонецПопытки;

	Возврат РеальныйТип;
КонецФункции

Функция ПрочитатьПрилепляемыеЧастицыВМетоде(Метод)

	ПрилепляемыеЧастицы = Новый Массив;
	Для Каждого ПараметрМетода Из Метод.Параметры Цикл

		ПрилепляемаяЧастица = ПрилеплениеЧастиц.ДанныеОПрилепляемойЧастице(ПараметрМетода);
		ПрилепляемыеЧастицы.Добавить(ПрилепляемаяЧастица);

	КонецЦикла;

	Возврат ПрилепляемыеЧастицы;

КонецФункции

Функция ПрочитатьХарактерЖелудя(Аннотации)
	ЗначениеПоУмолчанию = ХарактерыЖелудей.Одиночка();

	Аннотация = РаботаСАннотациями.НайтиАннотацию(Аннотации, "Характер");
	Если Аннотация = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;

	ХарактерЖелудя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
		Аннотация,
		,
		ЗначениеПоУмолчанию
	);

	Если НЕ ХарактерыЖелудей.ЭтоХарактерЖелудя(ХарактерЖелудя) Тогда
		ВызватьИсключение "Неизвестный характер желудя " + ХарактерЖелудя;
	КонецЕсли;

	Возврат ХарактерЖелудя;
КонецФункции

Функция ПрочитатьПрозвища(Аннотации, ЗначениеПоУмолчанию)
	
	Результат = Новый Массив;

	Прозвища = РаботаСАннотациями.НайтиАннотации(Аннотации, "Прозвище");
	Если Прозвища.Количество() = 0 Тогда
		Результат.Добавить(ЗначениеПоУмолчанию);
		Возврат Результат;
	КонецЕсли;

	Для Каждого Аннотация Из Прозвища Цикл
		Прозвище = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация);

		Результат.Добавить(Прозвище);
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПрочитатьПризнакВерховногоЖелудя(Аннотации)
	Возврат РаботаСАннотациями.НайтиАннотацию(Аннотации, "Верховный") <> Неопределено;
КонецФункции

Процедура ДобавитьОпределениеНапильника(ОпределениеНапильника, Системный = Ложь)

	Аннотация = РаботаСАннотациями.ПолучитьАннотацию(ОпределениеНапильника.Завязь().ДанныеМетода(), "Напильник");
	Порядок = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Порядок", 1);

	МинимальныйПорядок = 0;
	Если Порядок < МинимальныйПорядок Тогда
		ВызватьИсключение "Неверное значение параметра ""Порядок"". Порядок не может быть меньше, чем " + МинимальныйПорядок;
	КонецЕсли;

	МаксимальныйПорядок = 999999;
	Если Порядок > МаксимальныйПорядок Тогда
		ВызватьИсключение "Неверное значение параметра ""Порядок"". Порядок не может быть больше, чем " + МаксимальныйПорядок;
	КонецЕсли;

	Если НЕ Системный Тогда
		Если Порядок = МинимальныйПорядок ИЛИ Порядок = МаксимальныйПорядок Тогда
			ВызватьИсключение "Неверное значение параметра ""Порядок"". Использовано зарезервированное значение " + Порядок;
		КонецЕсли;
	КонецЕсли;

	СтрокаОпределений = ОпределенияНапильников.Добавить();
	СтрокаОпределений.Порядок = Порядок;
	СтрокаОпределений.Имя = ОпределениеНапильника.Имя();
	СтрокаОпределений.ОпределениеНапильника = ОпределениеНапильника;

	ОпределенияНапильников.Сортировать("Порядок Возр");

КонецПроцедуры

Процедура СохранитьОпределениеЖелудя(ОпределениеЖелудя)

	СохраненноеОпределениеЖелудя = ОпределенияЖелудейПоИмени.Получить(ОпределениеЖелудя.Имя());
	Если СохраненноеОпределениеЖелудя <> Неопределено Тогда
		Если ОпределениеЖелудя.Верховный() И СохраненноеОпределениеЖелудя.Верховный() Тогда
			ВызватьИсключение "Определение верховного желудя с именем """ + ОпределениеЖелудя.Имя() + """ уже существует";
		ИначеЕсли ОпределениеЖелудя.Верховный() И НЕ СохраненноеОпределениеЖелудя.Верховный() Тогда
			// no-op: Допустимая ситуация переопределения.
			// todo: Логирование
		ИначеЕсли НЕ ОпределениеЖелудя.Верховный() И СохраненноеОпределениеЖелудя.Верховный() Тогда
			// no-op: Допустимая ситуация непереопределения.
			// todo: Логирование
		Иначе
			ВызватьИсключение "Определение желудя с именем """ + ОпределениеЖелудя.Имя() + """ уже существует";
		КонецЕсли;
	КонецЕсли;

	ОпределенияЖелудейПоИмени.Вставить(ОпределениеЖелудя.Имя(), ОпределениеЖелудя);

	Прозвища = ОпределениеЖелудя.Прозвища();
	Для Каждого Прозвище Из Прозвища Цикл

		СуществующиеИмена = ОпределенияЖелудейПоПрозвищу.Получить(Прозвище);
		Если СуществующиеИмена = Неопределено Тогда
			СуществующиеИмена = Новый Массив;
		КонецЕсли;
		СуществующиеИмена.Добавить(ОпределениеЖелудя);
		ОпределенияЖелудейПоПрозвищу.Вставить(Прозвище, СуществующиеИмена);	

	КонецЦикла;

КонецПроцедуры

Процедура ПриСозданииОбъекта()
	ОпределенияЖелудейПоИмени = Новый Соответствие();
	ОпределенияЖелудейПоПрозвищу = Новый Соответствие();
	
	ОпределенияНапильников = Новый ТаблицаЗначений();
	ОпределенияНапильников.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
	ОпределенияНапильников.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	ОпределенияНапильников.Колонки.Добавить("ОпределениеНапильника", Новый ОписаниеТипов("ОпределениеЖелудя"));

	ОпределенияНапильников.Индексы.Добавить("Имя");
	ОпределенияНапильников.Индексы.Добавить("Порядок");

	ИнициализируемыеНапильники = Новый Соответствие();
	ОпределенияАннотаций = Новый Соответствие();
КонецПроцедуры