#Использовать logos

Перем _Рогатки;
Перем _ЗапускатьРогатки;
Перем _ЗапускатьРогаткиВФоне;

Перем Лог;

Процедура ЗапуститьПриложение() Экспорт
	
	Если НЕ _ЗапускатьРогатки Тогда
		Лог.Информация("Запуск рогаток отключен.");
		Возврат;
	КонецЕсли;

	КоличествоРогаток = _Рогатки.Количество() = 0;
	Если КоличествоРогаток Тогда
		Лог.Отладка("Список рогаток пуст.");
		Возврат;
	КонецЕсли;

	Лог.Отладка("Запускаю %1 рогаток.", КоличествоРогаток); 

	Если _ЗапускатьРогаткиВФоне Тогда
		ЗапуститьРогаткиВФоне();
	Иначе
		ЗапуститьРогаткиНепосредственно();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапуститьРогаткиВФоне()
	
	Лог.Отладка("Запуск рогаток в фоне.");

	МассивЗаданий = Новый Массив;
	Для Каждого Рогатка Из _Рогатки Цикл
		Лог.Отладка("Запускаю рогатку %1", Рогатка);
		ДлительнаяЗадача = Истина;
		ФоновоеЗадание = ФоновыеЗадания.Выполнить(Рогатка, "ПриЗапускеПриложения", , ДлительнаяЗадача);
	
		МассивЗаданий.Добавить(Новый Структура("Рогатка, Задание, ИнформацияВыведена", Рогатка, ФоновоеЗадание, Ложь));
	КонецЦикла;
		
	ТаймаутОжиданияФоновогоЗадания = 1000;
	ЕстьНеЗавершенные = Истина;
	Пока ЕстьНеЗавершенные Цикл

		ЕстьНеЗавершенные = Ложь;
		Для Каждого ДанныеЗадания Из МассивЗаданий Цикл
			
			Рогатка = ДанныеЗадания.Рогатка;
			ФоновоеЗадание = ДанныеЗадания.Задание;
			ИнформацияВыведена = ДанныеЗадания.ИнформацияВыведена;

			Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно И НЕ ИнформацияВыведена Тогда
				ДанныеЗадания.ИнформацияВыведена = Истина;
				Лог.Ошибка(
					"Ошибка запуска рогатки %1%2%3",
					Строка(Рогатка),
					Символы.ПС,
					ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке)
				);

				Продолжить;
			КонецЕсли;

			Если ФоновоеЗадание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
				Продолжить;
			КонецЕсли;
			
			ДождалисьЗавершения = ФоновоеЗадание.ОжидатьЗавершения(ТаймаутОжиданияФоновогоЗадания);
			ЕстьНеЗавершенные = ЕстьНеЗавершенные ИЛИ НЕ ДождалисьЗавершения;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

Процедура ЗапуститьРогаткиНепосредственно()

	Лог.Отладка("Запуск рогаток непосредственно");

	Для Каждого Рогатка Из _Рогатки Цикл
		Лог.Отладка("Запускаю рогатку %1", Строка(Рогатка));
		Рогатка.ПриЗапускеПриложения();
	КонецЦикла;

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(
	&Пластилин(Значение = "Рогатка", Тип = "Массив") Рогатки,
	&Деталька(ЗначениеПоУмолчанию = Истина) ЗапускатьРогатки,
	&Деталька(ЗначениеПоУмолчанию = Истина) ЗапускатьРогаткиВФоне
)
	
	_Рогатки = Рогатки;
	_ЗапускатьРогатки = ЗапускатьРогатки;
	_ЗапускатьРогаткиВФоне = ЗапускатьРогаткиВФоне;
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.autumn.ЗапускательПриложения");

КонецПроцедуры
