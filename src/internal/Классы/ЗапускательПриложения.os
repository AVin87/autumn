#Использовать logos

Перем _Поделка;
Перем _ЗапускатьРогатки;

Перем Лог;

Процедура ЗапуститьПриложение() Экспорт
	
	Если НЕ _ЗапускатьРогатки Тогда
		Лог.Информация("Запуск рогаток отключен.");
		Возврат;
	КонецЕсли;

	ОпределенияРогаток = _Поделка.ПолучитьОпределенияРогаток();

	КоличествоРогаток = ОпределенияРогаток.Количество() = 0;
	Если КоличествоРогаток Тогда
		Лог.Отладка("Список рогаток пуст.");
		Возврат;
	КонецЕсли;

	ОпределениеАннотацииРогатка = _Поделка.ПолучитьОпределенияАннотаций().Получить("рогатка");

	Лог.Отладка("Запускаю %1 рогаток.", КоличествоРогаток); 

	ЗапущенныеРогатки = Новый Массив;

	Для Каждого ДанныеОпределенияРогатки Из ОпределенияРогаток Цикл
		ОпределениеРогатки = ДанныеОпределенияРогатки.ОпределениеРогатки;
		Лог.Отладка("Запускаю рогатку %1", ОпределениеРогатки.Имя());

		Конструктор = ОпределениеРогатки.Завязь().ДанныеМетода();
		АннотацияРогатка = РаботаСАннотациями.ПолучитьАннотацию(Конструктор, "Рогатка");
		ОбъектАннотации = ОпределениеАннотацииРогатка.СоздатьОбъектАннотации(АннотацияРогатка);
		ЗапускатьВФоне = ОбъектАннотации.ЗапускатьВФоне();
		ОжидатьЗавершения = ОбъектАннотации.ОжидатьЗавершения();
		
		Рогатка = _Поделка.НайтиЖелудь(ОпределениеРогатки.Имя());

		Если ЗапускатьВФоне Тогда
			ДлительнаяЗадача = Истина;
			ФоновоеЗадание = ФоновыеЗадания.Выполнить(Рогатка, "ПриЗапускеПриложения", , ДлительнаяЗадача);

			Если ОжидатьЗавершения Тогда
				ЗапущенныеРогатки.Добавить(ФоновоеЗадание);
			КонецЕсли;
		Иначе
			Рогатка.ПриЗапускеПриложения();
		КонецЕсли;
	КонецЦикла;

	Если ЗапущенныеРогатки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Пока ЗапущенныеРогатки.Количество() > 0 Цикл
		ИндексЗадания = ФоновыеЗадания.ОжидатьЛюбое(ЗапущенныеРогатки);
		ФоновоеЗадание = ЗапущенныеРогатки[ИндексЗадания];

		Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			Лог.Ошибка(
				"Ошибка запуска рогатки %1%2%3",
				Строка(ФоновоеЗадание.Объект),
				Символы.ПС,
				ПодробноеПредставлениеОшибки(ФоновоеЗадание.ИнформацияОбОшибке)
			);
		КонецЕсли;

		ЗапущенныеРогатки.Удалить(ИндексЗадания);
	КонецЦикла;

	Лог.Отладка("Запуск приложения завершен");

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта(
	&Пластилин Поделка,
	&Деталька(Значение = "core.ЗапускатьРогатки", ЗначениеПоУмолчанию = Истина) ЗапускатьРогатки
)
	
	_Поделка = Поделка;
	_ЗапускатьРогатки = ЗапускатьРогатки;
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.autumn.ЗапускательПриложения");

КонецПроцедуры
