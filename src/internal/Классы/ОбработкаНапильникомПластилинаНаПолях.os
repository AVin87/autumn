#Использовать reflector

Перем _КонтекстПриложения;

Функция ОбработатьЖелудь(Желудь, ИмяЖелудя) Экспорт // BSLLS:UnusedParameters-off
	
	ОбработатьАннотацию("Пластилин", Желудь);
	ОбработатьАннотацию("Настройка", Желудь);
	
	Возврат Желудь;

КонецФункции

Функция ПолучитьКусокПластилина(Аннотация, МаркаПластилина)

	Если ВРег(Аннотация.Имя) = ВРег("Пластилин") Тогда

		ТипВнедряемогоЗначения = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, "Тип", "Желудь");

		// todo: в enum?
		Если ТипВнедряемогоЗначения = "Желудь" Тогда
			КусокПластилина = _КонтекстПриложения.ПолучитьЖелудь(МаркаПластилина);
		ИначеЕсли ТипВнедряемогоЗначения = "Массив" Тогда
			КусокПластилина = _КонтекстПриложения.ПолучитьЖелуди(МаркаПластилина);
		Иначе
			ВызватьИсключение "Неизвестный тип внедряемого через &Пластилин значения";
		КонецЕсли;

	ИначеЕсли ВРег(Аннотация.Имя) = ВРег("Настройка") Тогда

		ЗначениеПоУмолчанию = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			Аннотация, 
			"ЗначениеПоУмолчанию", 
			Неопределено,
			Истина
		);

		КусокПластилина = _КонтекстПриложения.ПолучитьНастройку(МаркаПластилина, ЗначениеПоУмолчанию);
	Иначе
		ВызватьИсключение "Неизвестная аннотация " + Аннотация.Имя();
	КонецЕсли;

	Возврат КусокПластилина;

КонецФункции

Процедура ОбработатьАннотацию(ИмяАннотации, Желудь)

	Рефлектор = Новый Рефлектор();
	РефлекторОбъекта = Новый РефлекторОбъекта(Желудь);
	
	Свойства = РефлекторОбъекта.ПолучитьТаблицуСвойств();
	Для Каждого Свойство Из Свойства Цикл
		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Свойство, ИмяАннотации);

		Если Аннотация = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		МаркаПластилина = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, , Свойство.Имя);
		КусокПластилина = ПолучитьКусокПластилина(Аннотация, МаркаПластилина);
		
		Рефлектор.УстановитьСвойство(Желудь, Свойство.Имя, КусокПластилина);

	КонецЦикла;

	Методы = РефлекторОбъекта.ПолучитьТаблицуМетодов(ИмяАннотации);
	Для Каждого Метод Из Методы Цикл
		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Метод, ИмяАннотации);

		ПрототипМаркиПластилина = СтрЗаменить(Метод.Имя, "Установить", "");

		МаркаПластилина = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(Аннотация, , ПрототипМаркиПластилина);
		КусокПластилина = ПолучитьКусокПластилина(Аннотация, МаркаПластилина);

		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(КусокПластилина);

		Рефлектор.ВызватьМетод(Желудь, Метод.Имя, ПараметрыМетода);
	КонецЦикла;

КонецПроцедуры

&Напильник(Порядок = 0)
Процедура ПриСозданииОбъекта(&Пластилин КонтекстПриложения)
	_КонтекстПриложения = КонтекстПриложения;
КонецПроцедуры
