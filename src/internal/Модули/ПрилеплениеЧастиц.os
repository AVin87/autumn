Функция ДанныеОПрилепляемойЧастице(Свойство, Знач ИмяСвойства = Неопределено) Экспорт

	Если ИмяСвойства = Неопределено Тогда
		ИмяСвойства = Свойство.Имя;
	КонецЕсли;

	АннотацияПластилин = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Пластилин");
	АннотацияДеталька = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Деталька");
	АннотацияБлестяшка = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Блестяшка");

	Если АннотацияПластилин <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Желудь();
		ИмяЧастицы = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			АннотацияПластилин,
			,
			ИмяСвойства
		);
		
		Аннотация = АннотацияПластилин;

	ИначеЕсли АннотацияДеталька <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Деталька();
		ИмяЧастицы = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			АннотацияДеталька,
			,
			ИмяСвойства
		);
		Аннотация = АннотацияДеталька;
	ИначеЕсли АннотацияБлестяшка <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Блестяшка();
		ИмяЧастицы = ИмяСвойства;
		Аннотация = АннотацияБлестяшка;
	Иначе
		ВызватьИсключение СтрШаблон(
			"Неизвестный тип прилепляемой частицы в свойстве/методе %1",
			ИмяСвойства
		);
	КонецЕсли;
	
	ПрилепляемаяЧастица = Новый ПрилепляемаяЧастица(ТипЧастицы, ИмяЧастицы, Аннотация);

	Возврат ПрилепляемаяЧастица;

КонецФункции

Функция НайтиПрилепляемуюЧастицу(Поделка, ДанныеОПрилепляемойЧастице) Экспорт

	ТипЧастицы = ДанныеОПрилепляемойЧастице.ТипЧастицы();
	ИмяЧастицы = ДанныеОПрилепляемойЧастице.ИмяЧастицы();
	Аннотация = ДанныеОПрилепляемойЧастице.Аннотация();

	Если ТипЧастицы = ТипыПрилепляемыхЧастиц.Желудь() Тогда

		ТипЖелудя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			Аннотация,
			"Тип",
			ТипыПрилепляемыхЖелудей.Желудь()
		);

		ПараметрыАннотации = РаботаСАннотациями.ПолучитьПараметрыАннотации(Аннотация, "Блестяшка");
		Параметры = Новый Массив;
		Для Каждого ПараметрБлестяшка Из ПараметрыАннотации Цикл
			Параметры.Добавить(ПараметрБлестяшка.Значение);
		КонецЦикла;

		ПоддерживаемыеТипыПрилепляемыхЖелудей = Новый Массив;
		ПоддерживаемыеТипыПрилепляемыхЖелудей.Добавить(ТипыПрилепляемыхЖелудей.Массив());
		ПоддерживаемыеТипыПрилепляемыхЖелудей.Добавить(ТипыПрилепляемыхЖелудей.Соответствие());
		
		Если ТипЖелудя = ТипыПрилепляемыхЖелудей.Желудь() Тогда
			ПрилепляемаяЧастица = Поделка.НайтиЖелудь(ИмяЧастицы, Параметры);
		ИначеЕсли ПоддерживаемыеТипыПрилепляемыхЖелудей.Найти(ТипЖелудя) >= 0 Тогда
			ПрилепляемаяЧастица = Поделка.НайтиЖелуди(ИмяЧастицы, Параметры, ТипЖелудя);
		Иначе
			ВызватьИсключение "Неизвестный тип внедряемого через &Пластилин значения";
		КонецЕсли;

	ИначеЕсли ТипЧастицы = ТипыПрилепляемыхЧастиц.Деталька() Тогда

		ЗначениеПоУмолчанию = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			Аннотация, 
			"ЗначениеПоУмолчанию", 
			Неопределено,
			Истина
		);

		ПрилепляемаяЧастица = Поделка.НайтиДетальку(ИмяЧастицы, ЗначениеПоУмолчанию);
	Иначе
		ВызватьИсключение "Неизвестная аннотация " + Аннотация.Имя();
	КонецЕсли;

	Возврат ПрилепляемаяЧастица;

КонецФункции