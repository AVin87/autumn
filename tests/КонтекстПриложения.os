
#Использовать ".."
#Использовать asserts
#Использовать "."

&Тест
Процедура ИмяКомпонентаАвтоматическиОпределяетсяИзТипа() Экспорт
	// Дано
	КонтекстПриложения = Новый КонтекстПриложения();
	
	// Когда
	КонтекстПриложения.ЗарегистрироватьКомпонент(Тип("ВерхнеУровневыйКомпонент"));

	// Тогда
	ОпределениеКомпонента = КонтекстПриложения.ПолучитьОпределениеКомпонента("ВерхнеУровневыйКомпонент");
	
	Ожидаем.Что(ОпределениеКомпонента.ТипКомпонента()).Равно(Тип("ВерхнеУровневыйКомпонент"));
	Ожидаем.Что(ОпределениеКомпонента.Имя()).Равно("ВерхнеУровневыйКомпонент");

КонецПроцедуры

&Тест
Процедура СоздаетсяПростойКомпонент() Экспорт
	// Дано
	КонтекстПриложения = Новый КонтекстПриложения();
	КонтекстПриложения.ЗарегистрироватьКомпонент(Тип("КомпонентНижнегоУровня"));
	
	// Когда
	Компонент = КонтекстПриложения.ПолучитьКомпонент("КомпонентНижнегоУровня");

	// Тогда
	Ожидаем.Что(Компонент).Не_().Равно(Неопределено);
КонецПроцедуры

&Тест
Процедура СоздаетсяКомпонентСЗависимостью() Экспорт
	// Дано
	КонтекстПриложения = Новый КонтекстПриложения();
	КонтекстПриложения.ЗарегистрироватьКомпонент(Тип("ВерхнеУровневыйКомпонент"));
	КонтекстПриложения.ЗарегистрироватьКомпонент(Тип("КомпонентНижнегоУровня"));
	
	// Когда
	Компонент = КонтекстПриложения.ПолучитьКомпонент("ВерхнеУровневыйКомпонент");

	// Тогда
	Ожидаем.Что(Компонент).Не_().Равно(Неопределено);

	Ожидаем.Что(Компонент.КомпонентНижнегоУровня()).Не_().Равно(Неопределено);
КонецПроцедуры

&Тест
Процедура КомпонентыИнициализируютсяКакСинглтоны() Экспорт
	// Дано
	КонтекстПриложения = Новый КонтекстПриложения();
	КонтекстПриложения.ЗарегистрироватьКомпонент(Тип("ВерхнеУровневыйКомпонент"));
	КонтекстПриложения.ЗарегистрироватьКомпонент(Тип("КомпонентНижнегоУровня"));
	
	// Когда
	ВерхнеУровневыйКомпонент = КонтекстПриложения.ПолучитьКомпонент("ВерхнеУровневыйКомпонент");
	КомпонентНижнегоУровня = КонтекстПриложения.ПолучитьКомпонент("КомпонентНижнегоУровня");

	// Тогда
	Ожидаем.Что(ВерхнеУровневыйКомпонент.КомпонентНижнегоУровня()).Равно(КомпонентНижнегоУровня);
КонецПроцедуры
